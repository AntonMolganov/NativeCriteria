plugins {
    id "io.spring.dependency-management" version "1.0.2.RELEASE"
    id 'pl.allegro.tech.build.axion-release' version '1.6.0'
    id "com.bmuschko.nexus" version "2.3.1" apply false
    id "io.codearte.nexus-staging" version "0.8.0" apply false
}

apply plugin: "pl.allegro.tech.build.axion-release"

scmVersion {
    localOnly = true
    hooks {
        pre 'fileUpdate', [files: ['README.md'], pattern: { v, p -> /'$v'/ }, replacement: { v, p -> "'$v'" }]
        pre 'commit'
    }
}

description "Native Criteria Root"

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://repo.spring.io/libs-snapshot'
        }
    }

    apply plugin:'java'
    apply plugin: 'maven'
    apply plugin: "io.spring.dependency-management"
    if (project.hasProperty('central')) {
        apply from: project.rootDir.path + '/gradle/mavenCentral.gradle'
    }

    group "com.github.pnowy.nc"
    version scmVersion.version

    dependencyManagement {
        dependencies {
            // CORE DEPENDENCIES
            dependency group: 'org.hibernate',              name: 'hibernate-core',         version: '5.2.10.Final'
            dependency group: 'com.google.guava',           name: 'guava',                  version: '21.0'
            dependency group: 'org.slf4j',                  name: 'slf4j-api',              version: '1.7.25'
            // SPRING MODULE
            dependency group: 'org.springframework.data',   name: 'spring-data-commons',    version: '2.0.2.RELEASE'
            dependency group: 'org.springframework',        name: 'spring-jdbc',            version: '4.3.8.RELEASE'
            // CORE TEST DEPENDENCIES
            dependency group: 'junit',                      name: 'junit',                  version: '4.12'
            dependency group: 'ch.qos.logback',             name: 'logback-classic',        version: '1.2.3'
            dependency group: 'org.assertj',                name: 'assertj-core',           version: '3.7.0'
            // DATABASES DRIVERS FOR TESTING
            dependency group: 'org.postgresql',             name: 'postgresql',             version: '42.0.0'
            dependency group: 'net.sourceforge.jtds',       name: 'jtds',                   version: '1.3.1'
            dependency group: 'mysql',                      name: 'mysql-connector-java',   version: '5.1.39'
        }
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:1.5.8.RELEASE'
        }
    }
}

subprojects {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding = 'UTF-8'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}
