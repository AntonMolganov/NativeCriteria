plugins {
    id "io.spring.dependency-management" version "1.0.2.RELEASE"
    id 'pl.allegro.tech.build.axion-release' version '1.6.0'
    id "com.jfrog.bintray" version "1.7.3"
}

scmVersion {
    localOnly = true
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'NativeCriteria'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/pnowy/NativeCriteria.git'
    }
    version {
        name = scmVersion.version
        released  = new Date()
        vcsTag = scmVersion.version
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
    apply plugin:'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: "io.spring.dependency-management"
    apply plugin: "pl.allegro.tech.build.axion-release"

    group "com.github.pnowy.nc"
    version scmVersion.version

    dependencyManagement {
        dependencies {
            dependency group: 'org.hibernate',              name: 'hibernate-core',         version: '4.3.11.Final'
            dependency group: 'com.google.guava',           name: 'guava',                  version: '18.0'
            dependency group: 'org.slf4j',                  name: 'slf4j-api',              version: '1.7.13'
            dependency group: 'org.testng',                 name: 'testng',                 version: '6.9.6'
            dependency group: 'org.assertj',                name: 'assertj-core',           version: '3.1.0'
            dependency group: 'ch.qos.logback',             name: 'logback-classic',        version: '1.1.3'
            dependency group: 'org.springframework.data',   name: 'spring-data-commons',    version: '1.11.2.RELEASE'
            dependency group: 'org.springframework',        name: 'spring-jdbc',            version: '4.2.4.RELEASE'

            dependency group: 'org.postgresql',             name: 'postgresql',             version: '42.0.0'
            dependency group: 'net.sourceforge.jtds',       name: 'jtds',                   version: '1.3.1'
            dependency group: 'mysql',                      name: 'mysql-connector-java',   version: '5.1.39'
        }
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:1.3.1.RELEASE'
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.github.pnowy.nc'
            artifactId 'nativeCriteria-root'
            version scmVersion.version
        }
    }
}

subprojects {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding = 'UTF-8'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4.1'
}
